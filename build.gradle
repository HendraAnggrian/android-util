buildscript {
    ext.versions = [
            // android
            sdk       : [
                    'min'   : 9,
                    'target': 25
            ],
            buildTools: '25.0.3',

            // main
            kotlin    : '1.1.3-2',
            support   : '25.4.0',

            // test
            runner    : '0.5',
            rules     : '0.5',
            espresso  : '2.2.2',
            junit     : '4.12',

            release   : '0.1.0'
    ]
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'
apply from: 'https://raw.githubusercontent.com/HendraAnggrian/hendraanggrian.github.io/master/bintray.gradle'

android {
    compileSdkVersion versions.sdk.target
    buildToolsVersion versions.buildTools
    defaultConfig {
        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    sourceSets {
        main.manifest.srcFile 'AndroidManifest.xml'
        main.java.srcDirs 'src'
        androidTest.setRoot('tests')
        androidTest.manifest.srcFile 'tests/AndroidManifest.xml'
        androidTest.java.srcDir 'tests/src'
        androidTest.res.srcDir 'tests/res'
        androidTest.resources.srcDir 'tests/src'
    }
}

repositories {
    maven { url "https://maven.google.com" }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    compile "com.android.support:support-compat:$versions.support"
    androidTestCompile "com.android.support:design:$versions.support"
    androidTestCompile("com.android.support.test:rules:$versions.rules") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:runner:$versions.runner") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:$versions.espresso") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile "junit:junit:$versions.junit"
}

tasks.withType(Javadoc) {
    excludes = ['**/*.kt']
}

publish {
    userOrg = bintray.user
    groupId = bintray.group
    artifactId = bintray.artifact.support_commons_utils.id
    publishVersion = versions.release
    desc = bintray.artifact.support_commons_utils.desc
    website = bintray.artifact.support_commons_utils.website
}

//$ ./gradlew bintrayUpload -PbintrayUser=hendraanggrian -PbintrayKey= -PdryRun=false